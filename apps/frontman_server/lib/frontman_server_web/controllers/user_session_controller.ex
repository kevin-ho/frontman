# Frontman Server
# Copyright (C) 2025 Frontman AI
#
# Licensed under the AGPL-3.0 — see LICENSE for details.
# Additional terms apply — see AI-SUPPLEMENTARY-TERMS.md

defmodule FrontmanServerWeb.UserSessionController do
  use FrontmanServerWeb, :controller

  alias FrontmanServer.Accounts
  alias FrontmanServer.Frameworks
  alias FrontmanServerWeb.UserAuth

  def new(conn, params) do
    # LOCAL-NOAUTH PATCH: in single-user local mode, visiting the login page
    # mints a real session for the local user instead of showing the OAuth
    # form. WorkOS is never consulted. Honors ?return_to= like the normal
    # login flow (safe_return_url? still applies inside log_in_user).
    local_user_id = Application.get_env(:frontman_server, :local_noauth_user_id)

    if local_user_id do
      case Accounts.get_user(local_user_id) do
        %Accounts.User{} = user ->
          conn
          |> maybe_put_user_return_to(params["return_to"])
          |> UserAuth.log_in_user(user, %{})

        nil ->
          render(conn, :new, form: Phoenix.Component.to_form(%{}, as: "user"))
      end
    else
    email = get_in(conn.assigns, [:current_scope, Access.key(:user), Access.key(:email)])
    form = Phoenix.Component.to_form(%{"email" => email}, as: "user")

    conn =
      conn
      |> maybe_put_user_return_to(params["return_to"])
      |> maybe_put_signup_framework(params["framework"])

    render(conn, :new, form: form)
  end
  end

  def create(conn, %{"user" => %{"token" => token} = user_params} = params) do
    info =
      case params do
        %{"_action" => "confirmed"} -> "User confirmed successfully."
        _ -> "Welcome back!"
      end

    case Accounts.login_user_by_magic_link(token) do
      {:ok, {user, _expired_tokens}} ->
        conn
        |> put_flash(:info, info)
        |> UserAuth.log_in_user(user, user_params)

      {:error, :not_found} ->
        conn
        |> put_flash(:error, "The link is invalid or it has expired.")
        |> render(:new, form: Phoenix.Component.to_form(%{}, as: "user"))
    end
  end

  def create(conn, %{"user" => %{"email" => email, "password" => password} = user_params}) do
    if user = Accounts.get_user_by_email_and_password(email, password) do
      conn
      |> put_flash(:info, "Welcome back!")
      |> UserAuth.log_in_user(user, user_params)
    else
      form = Phoenix.Component.to_form(user_params, as: "user")

      conn
      |> put_flash(:error, "Invalid email or password")
      |> render(:new, form: form)
    end
  end

  def create(conn, %{"user" => %{"email" => email}}) do
    if user = Accounts.get_user_by_email(email) do
      Accounts.deliver_login_instructions(
        user,
        &url(~p"/users/log-in/#{&1}")
      )
    end

    info =
      "If your email is in our system, you will receive instructions for logging in shortly."

    conn
    |> put_flash(:info, info)
    |> redirect(to: ~p"/users/log-in")
  end

  def confirm(conn, %{"token" => token}) do
    if user = Accounts.get_user_by_magic_link_token(token) do
      form = Phoenix.Component.to_form(%{"token" => token}, as: "user")

      conn
      |> assign(:user, user)
      |> assign(:form, form)
      |> render(:confirm)
    else
      conn
      |> put_flash(:error, "Magic link is invalid or it has expired.")
      |> redirect(to: ~p"/users/log-in")
    end
  end

  @doc """
  GET /users/log-out — renders a CSRF-protected confirmation page that auto-submits
  a DELETE form. This prevents forced-logout via `<img src="/users/log-out">` since
  the GET only returns HTML; the actual session destruction requires the DELETE with
  a valid CSRF token.
  """
  def confirm_logout(conn, params) do
    render(conn, :confirm_logout, return_to: params["return_to"])
  end

  def popup_complete(conn, _params), do: render(conn, :popup_complete)

  def delete(conn, params) do
    conn
    |> put_flash(:info, "Logged out successfully.")
    |> UserAuth.log_out_user(params["return_to"])
  end

  defp maybe_put_user_return_to(conn, nil), do: conn

  defp maybe_put_user_return_to(conn, return_to) when is_binary(return_to) do
    put_session(conn, :user_return_to, return_to)
  end

  defp maybe_put_user_return_to(conn, _), do: conn

  defp maybe_put_signup_framework(conn, framework) when is_binary(framework) do
    case Frameworks.valid_signup_id?(framework) do
      true -> put_session(conn, :signup_framework, framework)
      false -> delete_session(conn, :signup_framework)
    end
  end

  defp maybe_put_signup_framework(conn, _), do: delete_session(conn, :signup_framework)
end
